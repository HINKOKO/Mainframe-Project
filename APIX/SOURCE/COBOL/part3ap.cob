000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. PART3AP.
000300 AUTHOR. MATHIEU.
000301
000302******************************************************************
000303* THIS PROGRAM IS INTENDED TO :                                  *
000304*    - READ ORDERS AND ITEMS FROM DATABASE                       *
000305*    - FILLS EXTRACT.DATA IN ORDER TO EDIT BILLS                 *
000308******************************************************************
000382
001002 ENVIRONMENT DIVISION.
001003 CONFIGURATION SECTION.
001004 SPECIAL-NAMES.
001005     DECIMAL-POINT IS COMMA.
001006
001007 INPUT-OUTPUT SECTION.
001010 FILE-CONTROL.
001100     SELECT FEXT ASSIGN TO FICEXT.
001310
001320 DATA DIVISION.
001400 FILE SECTION.
001410* DECLARATION ENREGISTREMENTS FICHIER
001610 FD FEXT.
001620 01 ENR-FEXT PIC X(299).
001900
002000 WORKING-STORAGE SECTION.
002001******************************************
002002* INSERT DES DECLARATIONS SQL
002003     EXEC SQL INCLUDE SQLCA  END-EXEC.
002004     EXEC SQL INCLUDE ITE    END-EXEC.
002005     EXEC SQL INCLUDE PRO    END-EXEC.
002006     EXEC SQL INCLUDE ORD    END-EXEC.
002007     EXEC SQL INCLUDE CUS    END-EXEC.
002008     EXEC SQL INCLUDE EMP    END-EXEC.
002009     EXEC SQL INCLUDE DEP    END-EXEC.
002017******************************************
002018* DECLARATION DU CURSEUR ORDERS
002019     EXEC SQL
002020         DECLARE CURS CURSOR
002021         FOR
002022         SELECT
002023         I.P_NO, I.QUANTITY, I.PRICE,
002024         P.DESCRIPTION,
002025         O.O_NO, O.O_DATE,
002026         C.COMPANY, C.ADDRESS ,C.CITY ,C.ZIP, C.STATE,
002027         E.LNAME, E.FNAME, E.COM,
002028         D.DNAME
002029         FROM API10.ITEMS I
002030         JOIN API10.PRODUCTS P ON P.P_NO = I.P_NO
002031         JOIN API10.ORDERS O ON O.O_NO = I.O_NO
002032         JOIN API10.CUSTOMERS C ON C.C_NO = O.C_NO
002033         JOIN API10.EMPLOYEES E ON E.E_NO = O.S_NO
002034         JOIN API10.DEPTS D ON D.DEPT = E.DEPT
002035         ORDER BY O.O_NO ASC, I.P_NO ASC
002036     END-EXEC.
002037
002038* VARIABLES FONCTION ABEND-PROG
002039 77 WS-ANO PIC 9 VALUE ZERO.
002040 77 WS-VAR PIC 9 VALUE ZERO.
002041
002314* VARIABLES EDITION
002326 77 WR-ITE-QUANTITY PIC X(2).
002327* ITE-PRICE DCLGEN  PIC S9(3)V9(2) USAGE COMP-3.
002328 77 WR-ITE-PRICE    PIC X(6).
002329 77 WR-EMP-COM      PIC X(4).
002330 77 NC-ITE-PRICE    PIC S9(3)V9(2).
002331 77 ED-ITE-PRICE    PIC 999V99.
002332 77 ED-EMP-COM      PIC 9V99.
002333 77 WR-ORD-O-NO     PIC X(3).
002340 77 WR-DATAF        PIC X(299).
002444
002445 01 LIGNE.
002446    05 L-P-NO        PIC X(3) .
002447    05 L-QUANTITY    PIC X(2) .
002448    05 L-PRICE       PIC X(6) .
002449    05 L-DESCRIPTION PIC X(30) .
002450    05 L-O-NO        PIC X(3) .
002451    05 L-O-DATE      PIC X(10).
002452    05 L-COMPANY     PIC X(30) .
002453    05 L-ADDRESS     PIC X(100) .
002454    05 L-CITY        PIC X(20) .
002455    05 L-ZIP         PIC X(5) .
002456    05 L-STATE       PIC X(2) .
002457    05 L-LNAME       PIC X(20).
002458    05 L-FNAME       PIC X(20).
002459    05 L-COM         PIC X(4).
002460    05 L-DNAME       PIC X(20).
002461    05 FILLER        PIC X(28).
002462* LONGUEUR = 270 + FILLER 29 --> FICHIER 300
002463
002464 PROCEDURE DIVISION.
002465* PARTIE 1 ALIMENTATION DU FICHIER EXTRACT.DATA
002470      PERFORM OPEN-FEXT
002700      PERFORM OPEN-CURS
002710      PERFORM FETCH-CURS
002800      PERFORM TEST-SQLCODE
003000      PERFORM UNTIL SQLCODE NOT EQUAL ZERO
003010         PERFORM WRITE-FEXT
003020         PERFORM FETCH-CURS
003030         PERFORM TEST-SQLCODE
003700      END-PERFORM
003900      PERFORM CLOSE-CURS
004000      PERFORM CLOSE-FEXT
004010* PARTIE 2 ALIMENTATION DU FICHIER FACTURES.DATA
004100      GOBACK.
004110********************************************
004120* PARAGRAPHES
004200********************************************
004760 CLOSE-FEXT.
004770     CLOSE FEXT.
004780 OPEN-FEXT.
004790     OPEN OUTPUT FEXT.
004791 CLOSE-CURS.
004792     EXEC SQL CLOSE CURS END-EXEC.
004793 OPEN-CURS.
004794     EXEC SQL OPEN CURS END-EXEC.
004795 FETCH-CURS.
004796     EXEC SQL
004797         FETCH CURS
004798         INTO
004799         :ITE-P-NO,
004800         :ITE-QUANTITY,
004801         :ITE-PRICE,
004802         :PRO-DESCRIPTION,
004803         :ORD-O-NO,
004804         :ORD-O-DATE,
004805         :CUS-COMPANY,
004806         :CUS-ADDRESS,
004807         :CUS-CITY,
004808         :CUS-ZIP,
004809         :CUS-STATE,
004810         :EMP-LNAME,
004811         :EMP-FNAME,
004812         :EMP-COM,
004813         :DEP-DNAME
004814     END-EXEC.
004815 TEST-SQLCODE.
004816     EVALUATE TRUE
004817         WHEN SQLCODE = ZERO
004818              CONTINUE
004819         WHEN SQLCODE > ZERO
004820              DISPLAY 'WARNING : ' SQLCODE
004821         WHEN OTHER
004822              DISPLAY 'ANOMALIE GRAVE : ' SQLCODE
004823              PERFORM ABEND-PROG
004824     END-EVALUATE.
004829 ABEND-PROG.
004830     COMPUTE WS-ANO = WS-ANO / WS-VAR.
009000 WRITE-FEXT.
009100* TRAITEMENT COMPATIBILITE DONNEES
009110     MOVE ITE-QUANTITY      TO WR-ITE-QUANTITY
009122     MOVE ITE-PRICE         TO ED-ITE-PRICE
009123     MOVE EMP-COM           TO ED-EMP-COM
009124     MOVE ED-ITE-PRICE(1:3) TO WR-ITE-PRICE(1:3)
009125     MOVE ','               TO WR-ITE-PRICE(4:1)
009126     MOVE ED-ITE-PRICE(4:2) TO WR-ITE-PRICE(5:2)
009127     MOVE ED-EMP-COM(1:1)   TO WR-EMP-COM(1:1)
009128     MOVE ','               TO WR-EMP-COM(2:1)
009129     MOVE ED-EMP-COM(2:2)   TO WR-EMP-COM(3:2)
009130     MOVE ORD-O-NO          TO WR-ORD-O-NO
009140* ALIM LIGNE AVEC VARIABLES DU FETCH
009150     MOVE ITE-P-NO                TO L-P-NO
009160     MOVE WR-ITE-QUANTITY         TO L-QUANTITY
009170     MOVE WR-ITE-PRICE            TO L-PRICE
009180     MOVE PRO-DESCRIPTION-TEXT(1:PRO-DESCRIPTION-LEN)
009181                                  TO L-DESCRIPTION
009190     MOVE WR-ORD-O-NO             TO L-O-NO
009191     MOVE ORD-O-DATE              TO L-O-DATE
009192     MOVE CUS-COMPANY-TEXT(1:CUS-COMPANY-LEN)     TO L-COMPANY
009193     MOVE CUS-ADDRESS-TEXT(1:CUS-ADDRESS-LEN)     TO L-ADDRESS
009194     MOVE CUS-CITY-TEXT(1:CUS-CITY-LEN)           TO L-CITY
009195     MOVE CUS-ZIP                 TO L-ZIP
009196     MOVE CUS-STATE               TO L-STATE
009197     MOVE EMP-LNAME-TEXT(1:EMP-LNAME-LEN)         TO L-LNAME
009198     MOVE EMP-FNAME-TEXT(1:EMP-FNAME-LEN)         TO L-FNAME
009199     MOVE WR-EMP-COM              TO L-COM
009200     MOVE DEP-DNAME-TEXT(1:DEP-DNAME-LEN)         TO L-DNAME
009300     WRITE ENR-FEXT FROM LIGNE BEFORE ADVANCING 1 LINES
009301     PERFORM CLEAN-L
009310     .
009400 CLEAN-L.
009410     MOVE SPACES TO L-P-NO
009420     MOVE SPACES TO L-QUANTITY
009430     MOVE SPACES TO L-PRICE
009440     MOVE SPACES TO L-DESCRIPTION
009450     MOVE SPACES TO L-O-NO
009460     MOVE SPACES TO L-O-DATE
009470     MOVE SPACES TO L-COMPANY
009480     MOVE SPACES TO L-ADDRESS
009490     MOVE SPACES TO L-CITY
009491     MOVE SPACES TO L-ZIP
009492     MOVE SPACES TO L-STATE
009493     MOVE SPACES TO L-LNAME
009494     MOVE SPACES TO L-FNAME
009495     MOVE SPACES TO L-COM
009496     MOVE SPACES TO L-DNAME
009500     .
