000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. PART3BP.
000210 AUTHOR. MATHIEU.
000300
000310******************************************************************
000320* THIS PROGRAM IS INTENDED TO :                                  *
000330*    - READ EXTRACT.DATA                                         *
000340*    - FILLS FACTURES.DATA WITH BILLS OF EACH ORDER              *
000370******************************************************************
000391
000400 ENVIRONMENT DIVISION.
000500 CONFIGURATION SECTION.
000600 SPECIAL-NAMES.
000700     DECIMAL-POINT IS COMMA.
000800
000900 INPUT-OUTPUT SECTION.
001000 FILE-CONTROL.
001100     SELECT FEXT ASSIGN TO FICEXT.
001200     SELECT FACT ASSIGN TO FICFACT.
001310
001320 DATA DIVISION.
001400 FILE SECTION.
001410* DECLARATION ENREGISTREMENTS FICHIER
001610 FD FEXT.
001620 01 ENR-FEXT.
001630    05 FILLER        PIC X.
001910    05 F-P-NO        PIC X(3).
001920    05 F-QUANTITY    PIC X(2).
001930    05 F-PRICE       PIC X(6).
001940    05 F-DESCRIPTION PIC X(30).
001950    05 F-O-NO        PIC X(3).
001960    05 F-O-DATE      PIC X(10).
001970    05 F-COMPANY     PIC X(30).
001980    05 F-ADDRESS     PIC X(100).
001990    05 F-CITY        PIC X(20).
001991    05 F-ZIP         PIC X(5).
001992    05 F-STATE       PIC X(2).
001993    05 F-LNAME       PIC X(20).
001994    05 F-FNAME       PIC X(20).
001995    05 F-COM         PIC X(4).
001996    05 F-DNAME       PIC X(20).
001997    05 FILLER        PIC X(24).
001998
001999 FD FACT.
002000 01 ENR-FACT.
002001    05 FILLER        PIC X.
002002    05 FILLER        PIC X(79).
002003
002010 WORKING-STORAGE SECTION.
002039
002040* VARIABLES FONCTION ABEND-PROG
002041 77 WS-ANO PIC 9 VALUE ZERO.
002042 77 WS-VAR PIC 9 VALUE ZERO.
002043* VARIABLES FLAG FICHIER
002044 77 WS-FLAG PIC 9 VALUE ZERO.
002045    88 FF-FEXT VALUE 1.
002314* VARIABLES CALCUL
002315* VARIABLES ORDER ITEM
002326 77 WS-TOTAL       PIC 9(4)V9(2).
002328 77 WS-QTY         PIC 9(2).
002329 77 WS-PRICE       PIC 9(3)V9(2).
002330 77 TP-PRICE       PIC 9(3)V9(2).
002331 77 WS-COM         PIC 9(2)V9(2).
002332 77 TP-COM         PIC 9(2)V9(2).
002333 77 WS-TP-COM      PIC 9(2)V9(2).
002334 77 WS-COM-LIGNE   PIC 9(2)V9(2).
002335* VARIABLES TOTAUX ORDER
002336 77 WS-TOTAL-HT    PIC 9(4)V9(2).
002337 77 WS-TOTAL-TTC   PIC 9(4)V9(2).
002338 77 WS-TOTAL-COM   PIC 9(4)V9(2).
002339 77 WS-TOTAL-TAX   PIC 9(4)V9(2).
002340 77 AC-TAX         PIC X(5).
002341 77 WS-TAX         PIC 9(2)V9(2).
002342 77 TP-TAX         PIC 9(2)V9(2).
002343 77 WS-TP-TAX      PIC 9(2)V9(2).
002344* VARIABLES EDITION
002345 77 ED-MONTANT       PIC ZZ9,9(2).
002346 77 ED-TOTAL         PIC ZZZ9,9(2).
002347 77 ED-QTY           PIC 99.
002348 77 ED-COM           PIC Z9,99.
002349 77 ED-TAX           PIC Z9,99.
002350 77 ED-CONTACT       PIC X(70).
002351* VARIABLES CURRENT
002360 77 WS-CUR-O-NO      PIC X(3).
002361 77 WS-CUR-COM       PIC X(4).
002370* VARIABLES SOUS PROGRAMMES
002380 77 LS-DATE          PIC X(30).
002390 77 LS-ST-CODE       PIC X(2).
002444 77 LS-ST-NAME       PIC X(30).
002445
002446 01 L-TIRET.
002460    05 FILLER  PIC X(80) VALUE ALL '-'.
002461
002462 01 L-TIRET-CUS.
002463    05 FILLER  PIC X(3)  VALUE     '|  '.
002464    05 FILLER  PIC X(40) VALUE ALL ' '.
002465    05 FILLER  PIC X(34) VALUE ALL '-'.
002466    05 FILLER  PIC X(3)  VALUE     '  |'.
002467
002468 01 L-TIRET-ITE.
002470    05 FILLER  PIC X(3)  VALUE     '|  '.
002472    05 FILLER  PIC X(74) VALUE ALL '-'.
002474    05 FILLER  PIC X(3)  VALUE     '  |'.
002475
002476 01 L-VIDE.
002477    05 FILLER  PIC X(3)  VALUE     '|  '.
002478    05 FILLER  PIC X(74) VALUE ALL ' '.
002479    05 FILLER  PIC X(3)  VALUE     '  |'.
002480
002481 01 L-TOTAL.
002482    05 FILLER      PIC X(3)  VALUE     '|  '.
002483    05 FILLER      PIC X(40) VALUE ALL ' '.
002484    05 L-TOT-TITLE PIC X(27).
002485    05 L-TOT-DATA  PIC ZZZZ,99.
002486    05 FILLER      PIC X(3)  VALUE     '  |'.
002487
002488 01 L-TITLE-ITE.
002489    05 FILLER  PIC X(4)  VALUE '|  |'.
002490    05 FILLER  PIC X(8)  VALUE 'P_NO   |'.
002492    05 FILLER  PIC X(12) VALUE 'DESCRIPTION'.
002493    05 FILLER  PIC X(17) VALUE ALL ' '.
002494    05 FILLER  PIC X     VALUE '|'.
002495    05 FILLER  PIC X(10) VALUE 'QUANTITY |'.
002496    05 FILLER  PIC X(8)  VALUE 'PRICE  |'.
002497    05 FILLER  PIC X(16) VALUE 'TOTAL LIGNE     '.
002498    05 FILLER  PIC X(4)  VALUE              '|  |'.
002499
002500 01 L-ITE.
002501    05 FILLER    PIC X(4)  VALUE     '|  |'.
002502    05 L-ITE-NO  PIC X(7).
002503    05 FILLER    PIC X     VALUE     '|'.
002504    05 L-ITE-DES PIC X(29).
002505    05 FILLER    PIC X     VALUE     '|'.
002506    05 FILLER    PIC X(7)  VALUE ALL ' '.
002507    05 L-ITE-QTY PIC X(2).
002508    05 FILLER    PIC X     VALUE     '|'.
002509    05 FILLER    PIC X     VALUE ALL ' '.
002510    05 L-ITE-PRI PIC ZZZ,99.
002511    05 FILLER    PIC X     VALUE     '|'.
002512    05 FILLER    PIC X(9)  VALUE ALL ' '.
002513    05 L-ITE-TOT PIC ZZZZ,99.
002514    05 FILLER    PIC X(4)  VALUE     '|  |'.
002515
002516 01 L-DATE.
002517    05 FILLER  PIC X(3)  VALUE     '|  '.
002518    05 FILLER  PIC X(3)  VALUE     '   '.
002519    05 L-DATE-TITLE PIC X(10).
002520    05 L-DATE-DATA PIC X(30).
002521    05 FILLER  PIC X(31) VALUE ALL ' '.
002522    05 FILLER  PIC X(3)  VALUE     '  |'.
002523
002524 01 L-ORD.
002525    05 FILLER  PIC X(3)  VALUE     '|  '.
002526    05 L-ORD-TITLE PIC X(12).
002527    05 L-ORD-DATA  PIC X(10).
002528    05 FILLER  PIC X(52) VALUE ALL ' '.
002529    05 FILLER  PIC X(3)  VALUE     '  |'.
002530
002531 01 L-EMP-DEUX-LIGNES.
002532    05 FILLER  PIC X(3)  VALUE     '|  '.
002533    05 L-EMP-DATA-LNAME PIC X(20).
002534    05 FILLER  PIC X VALUE ALL ','.
002535    05 L-EMP-DATA-FNAME PIC X(20).
002536    05 FILLER  PIC X(33) VALUE ALL ' '.
002537    05 FILLER  PIC X(3)  VALUE     '  |'.
002538
002539 01 L-EMP-UNE-LIGNE.
002540    05 FILLER  PIC X(3)  VALUE     '|  '.
002541    05 L-EMP-DATA PIC X(74).
002543    05 FILLER  PIC X(3)  VALUE     '  |'.
002544
002545 01 L-DEP.
002546    05 FILLER  PIC X(3)  VALUE     '|  '.
002547    05 L-DEP-TITLE PIC X(30).
002548    05 L-DEP-DATA PIC X(20).
002549    05 FILLER  PIC X(3) VALUE ALL ' : '.
002550    05 FILLER  PIC X(21) VALUE ALL ' '.
002551    05 FILLER  PIC X(3)  VALUE     '  |'.
002552
002553 01 L-CUS.
002554    05 FILLER       PIC X(3)  VALUE     '|  '.
002555    05 FILLER       PIC X(40) VALUE ALL ' '.
002557    05 FILLER       PIC X(2) VALUE '| '.
002559    05 FILLER       PIC X(2) VALUE '  '.
002560    05 L-CUS-DATA   PIC X(28).
002561    05 FILLER       PIC X(2) VALUE ' |'.
002563    05 FILLER       PIC X(3)  VALUE     '  |'.
002566
002570 PROCEDURE DIVISION.
002580
002581* RECUPERATION DU TAUX DE TAXE DANS LA SYSIN
002590      ACCEPT AC-TAX FROM SYSIN
002591
002592* OUVERTURE DES FICHIERS
002600      PERFORM OPEN-FEXT
002710      PERFORM READ-FEXT
002720      PERFORM OPEN-FACT
002730
002731      PERFORM INIT-ORDER
002740      PERFORM WRITE-FACT-TOP
002750      PERFORM WRITE-FACT-ITE-TOP
002900
003000      PERFORM UNTIL FF-FEXT
003013
003014         PERFORM WRITE-FACT-ITE UNTIL FF-FEXT OR
003015                       F-O-NO NOT EQUAL WS-CUR-O-NO
003017
003034         PERFORM WRITE-FACT-ITE-BOTTOM
003035         PERFORM WRITE-FACT-BOTTOM
003040         PERFORM INIT-ORDER
003041         IF NOT FF-FEXT
003042            PERFORM WRITE-FACT-TOP
003043            PERFORM WRITE-FACT-ITE-TOP
003050         END-IF
003700      END-PERFORM
004000      PERFORM CLOSE-FEXT
004010      PERFORM CLOSE-FACT
004100      GOBACK.
004110******************************************
004120* PARAGRAPHES
004200******************************************
004760 CLOSE-FEXT.
004770     CLOSE FEXT.
004780 OPEN-FEXT.
004790     OPEN INPUT FEXT.
004791 CLOSE-FACT.
004792     CLOSE FACT.
004793 OPEN-FACT.
004794     OPEN OUTPUT FACT.
004800 READ-FEXT.
004810     READ FEXT AT END
004820         SET FF-FEXT TO TRUE
004821         DISPLAY 'FICHIER EXTRACT VIDE OU FINI'
004822     END-READ.
004829 ABEND-PROG.
004830     COMPUTE WS-ANO = WS-ANO / WS-VAR.
004831******************************************
004840* PARAGRAPHES DIVERS WRITE
004850******************************************
004860 WRITE-FACT-ITE-TOP.
004861     DISPLAY L-TIRET-ITE
004870     DISPLAY L-TITLE-ITE
004871     DISPLAY L-TIRET-ITE
004872     WRITE ENR-FACT FROM L-TIRET-ITE
004873     WRITE ENR-FACT FROM L-TITLE-ITE
004874     WRITE ENR-FACT FROM L-TIRET-ITE
004880     .
004890 WRITE-FACT-ITE-BOTTOM.
004900     DISPLAY L-TIRET-ITE
004910     WRITE ENR-FACT FROM L-TIRET-ITE
005000     .
009006     .
009007 WRITE-FACT-ITE.
009008* CALCULS ET CAST
009009     MOVE F-QUANTITY     TO WS-QTY
009010     COMPUTE TP-PRICE = FUNCTION NUMVAL(F-PRICE)
009011     MOVE    TP-PRICE   TO WS-PRICE
009012     COMPUTE TP-COM   = FUNCTION NUMVAL(F-COM)
009013     MOVE    TP-COM     TO WS-COM
009014     COMPUTE WS-TOTAL = WS-QTY * WS-PRICE
009015     COMPUTE WS-COM-LIGNE = WS-TOTAL * WS-COM
009018     MOVE WS-TOTAL       TO ED-TOTAL
009019     MOVE WS-PRICE       TO ED-MONTANT
009020     MOVE WS-QTY         TO ED-QTY
009021     MOVE WS-COM         TO ED-COM
009023* TRAITEMENT TOTAUX
009024     ADD WS-TOTAL TO WS-TOTAL-HT
009025     ADD WS-COM-LIGNE TO WS-TOTAL-COM
009026* MOVE DANS LIGNE
009027     MOVE F-P-NO         TO L-ITE-NO
009028     MOVE SPACES TO L-ITE-DES
009029     MOVE F-DESCRIPTION  TO L-ITE-DES
009030     MOVE SPACES TO F-DESCRIPTION
009031     MOVE ED-QTY         TO L-ITE-QTY
009032     MOVE ED-MONTANT     TO L-ITE-PRI
009033     MOVE ED-TOTAL       TO L-ITE-TOT
009034     DISPLAY L-ITE
009035     WRITE ENR-FACT FROM L-ITE
009036     PERFORM CLEAN-L-ITE
009038     PERFORM INIT-ITEM
009040     PERFORM READ-FEXT
009410     .
009500 WRITE-FACT-CUS.
009501* COMPANY NAME
009510     WRITE ENR-FACT FROM L-TIRET-CUS
009512     MOVE F-COMPANY   TO L-CUS-DATA
009513     DISPLAY L-CUS
009514     WRITE ENR-FACT FROM L-CUS
009515* COMPANY ADDRESS
009518     MOVE F-ADDRESS   TO L-CUS-DATA
009519     DISPLAY L-CUS
009520     WRITE ENR-FACT FROM L-CUS
009521* COMPANY CITY ZIP
009525     STRING
009526          F-CITY DELIMITED BY '  '
009527          ', '   DELIMITED BY SIZE
009528          F-ZIP  DELIMITED BY SIZE
009529          INTO L-CUS-DATA
009530     END-STRING
009531     DISPLAY L-CUS
009532     WRITE ENR-FACT FROM L-CUS
009533* COMPANY STATE
009534     PERFORM CALL-SSP-STATE
009535     MOVE LS-ST-NAME  TO L-CUS-DATA
009537     DISPLAY L-CUS
009538     WRITE ENR-FACT FROM L-CUS
009539     DISPLAY L-TIRET-CUS
009540     WRITE ENR-FACT FROM L-TIRET-CUS
009542     MOVE SPACES TO F-STATE
009543     .
009561 CLEAN-L-EMP.
009562     MOVE SPACES TO L-DEP-DATA
009563     MOVE SPACES TO L-EMP-DATA-FNAME
009564     MOVE SPACES TO L-EMP-DATA-LNAME
009565     MOVE SPACES TO L-EMP-DATA
009566     .
009570 CLEAN-L-ITE.
009580     MOVE SPACES TO L-ITE-NO
009581     MOVE SPACES TO L-ITE-DES
009582     MOVE SPACES TO L-ITE-QTY
009583     MOVE 0 TO L-ITE-PRI
009584     MOVE 0 TO L-ITE-TOT
009590     .
009598 CLEAN-L-DAT.
009599     MOVE SPACES TO L-DATE-DATA
009600     .
009601 CLEAN-L-ORD.
009602     MOVE SPACES TO L-ORD-DATA
009603     .
009604 WRITE-FACT-DAT.
009605     MOVE 'PARIS, LE ' TO L-DATE-TITLE
009606* SOUS PROGRAMME DATE
009608     PERFORM CALL-SSP-DATE
009609     MOVE FUNCTION LOWER-CASE(LS-DATE) TO L-DATE-DATA
009611     DISPLAY L-DATE
009612     WRITE ENR-FACT FROM L-DATE
009613     PERFORM CLEAN-L-DAT
009620     .
009700 WRITE-FACT-ORD.
009701     MOVE 'ORDER NUM  :' TO L-ORD-TITLE
009703     MOVE F-O-NO TO L-ORD-DATA
009704     DISPLAY L-ORD
009705     WRITE ENR-FACT FROM L-ORD
009706     MOVE 'ORDER DATE :' TO L-ORD-TITLE
009707     MOVE F-O-DATE TO L-ORD-DATA
009708     DISPLAY L-ORD
009709     WRITE ENR-FACT FROM L-ORD
009710     PERFORM CLEAN-L-ORD
009720     .
009800 WRITE-FACT-EMP.
009810* CONSTRUCTION PHRASE CONTACT
009813     STRING
009814          'V' DELIMITED BY SIZE
009815          'otre contact au département ' delimited by size
009816          F-DNAME  DELIMITED BY ' '
009817          ' : '    DELIMITED BY SIZE
009818          F-LNAME  DELIMITED BY ' '
009819          ', '     DELIMITED BY SIZE
009820          F-FNAME  DELIMITED BY ' '
009821     INTO ED-CONTACT
009822     END-STRING
009823     DISPLAY '--' ED-CONTACT '--'
009824     MOVE ED-CONTACT TO L-EMP-DATA
009825     WRITE ENR-FACT FROM L-EMP-UNE-LIGNE
009826     PERFORM CLEAN-L-EMP
009827     .
009830 WRITE-FACT-BOTTOM.
009832     COMPUTE TP-TAX = FUNCTION NUMVAL(AC-TAX)
009833     MOVE    TP-TAX   TO WS-TAX
009836     COMPUTE WS-TP-TAX = WS-TAX + 1
009837     COMPUTE WS-TOTAL-TAX = WS-TOTAL-HT * WS-TAX
009838     COMPUTE WS-TOTAL-TTC = WS-TOTAL-HT * WS-TP-TAX
009845     MOVE 'TOTAL HT'           TO L-TOT-TITLE
009846     MOVE WS-TOTAL-HT          TO L-TOT-DATA
009848     WRITE ENR-FACT FROM L-VIDE
009849     WRITE ENR-FACT FROM L-TOTAL
009850     WRITE ENR-FACT FROM L-VIDE
009851* TRAITEMENT TOTAL-TAX
009852     COMPUTE WS-TAX = WS-TAX * 100
009853     MOVE WS-TAX TO ED-TAX
009856     STRING
009857          'TOTAL TAXE (' DELIMITED BY SIZE
009858          ED-TAX  DELIMITED BY SIZE
009859          '%)' DELIMITED BY SIZE
009860          INTO L-TOT-TITLE
009861     END-STRING
009863     MOVE WS-TOTAL-TAX TO L-TOT-DATA
009865     WRITE ENR-FACT FROM L-TOTAL
009866     WRITE ENR-FACT FROM L-VIDE
009867* TRAITEMENT TOTAL-COM
009868     COMPUTE TP-COM = FUNCTION NUMVAL(WS-CUR-COM)
009869     MOVE    TP-COM   TO WS-COM
009871     COMPUTE WS-TP-COM = WS-COM + 1
009872     COMPUTE WS-TOTAL-COM = WS-TOTAL-HT * WS-COM
009875     COMPUTE WS-COM = WS-COM * 100
009876     MOVE WS-COM TO ED-COM
009880     STRING
009881          'COMMISSION (' DELIMITED BY SIZE
009882          ED-COM  DELIMITED BY SIZE
009883          '%)' DELIMITED BY SIZE
009884          INTO L-TOT-TITLE
009885     END-STRING
009887     MOVE WS-TOTAL-COM         TO L-TOT-DATA
009889     WRITE ENR-FACT FROM L-TOTAL
009890     WRITE ENR-FACT FROM L-VIDE
009891     MOVE 'TOTAL TTC'          TO L-TOT-TITLE
009892     MOVE WS-TOTAL-TTC         TO L-TOT-DATA
009894     WRITE ENR-FACT FROM L-TOTAL
009895     WRITE ENR-FACT FROM L-VIDE
009896     WRITE ENR-FACT FROM L-TIRET BEFORE ADVANCING PAGE
009897     .
009898 WRITE-FACT-TOP.
009899     WRITE   ENR-FACT FROM L-TIRET
009900     WRITE   ENR-FACT FROM L-VIDE
009903     PERFORM WRITE-FACT-CUS
009904     PERFORM WRITE-FACT-DAT
009905     WRITE   ENR-FACT FROM L-VIDE
009906     PERFORM WRITE-FACT-ORD
009907     PERFORM WRITE-FACT-EMP
009908     WRITE   ENR-FACT FROM L-VIDE
009909     WRITE   ENR-FACT FROM L-VIDE
009910     .
009911 INIT-ORDER.
009912     MOVE F-O-NO TO WS-CUR-O-NO
009913     MOVE F-COM  TO WS-CUR-COM
009914     MOVE ZERO TO WS-TOTAL-HT
009915     MOVE ZERO TO WS-TOTAL-TTC
009916     MOVE ZERO TO WS-TOTAL-COM
009930     MOVE ZERO TO WS-TOTAL
009940     MOVE ZERO TO WS-COM-LIGNE
009950     MOVE ZERO TO WS-QTY
009960     MOVE ZERO TO WS-PRICE
009970     MOVE ZERO TO TP-PRICE
009980     MOVE ZERO TO WS-COM
009990     MOVE ZERO TO TP-COM
010000     .
010010 INIT-ITEM.
010020     MOVE F-O-NO TO WS-CUR-O-NO
010030     MOVE ZERO TO WS-TOTAL
010040     MOVE ZERO TO WS-COM-LIGNE
010041     MOVE ZERO TO WS-QTY
010042     MOVE ZERO TO WS-PRICE
010043     MOVE ZERO TO TP-PRICE
010044     MOVE ZERO TO WS-COM
010045     MOVE ZERO TO TP-COM
010055     .
010060 CLEAN-F.
010400     MOVE SPACES TO F-P-NO
010500     MOVE SPACES TO F-QUANTITY
010600     MOVE SPACES TO F-PRICE
010700     MOVE SPACES TO F-DESCRIPTION
010800     MOVE SPACES TO F-O-NO
010900     MOVE SPACES TO F-O-DATE
011000     MOVE SPACES TO F-COMPANY
011100     MOVE SPACES TO F-ADDRESS
011200     MOVE SPACES TO F-CITY
011300     MOVE SPACES TO F-ZIP
011400     MOVE SPACES TO F-STATE
011500     MOVE SPACES TO F-LNAME
011600     MOVE SPACES TO F-FNAME
011700     MOVE SPACES TO F-DNAME
011800     .
011900 CALL-SSP-DATE.
012000     CALL 'DATEPROG' USING LS-DATE
012100     .
012200 CALL-SSP-STATE.
012210     MOVE F-STATE TO LS-ST-CODE
012300     CALL 'STPROG' USING LS-ST-CODE LS-ST-NAME
012400     .
