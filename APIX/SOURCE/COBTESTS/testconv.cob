000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. TESTCONV.
000300 AUTHOR.     HINKOKO.
000400***************************************************
000500*  THIS PROGRAM IS INTENDED TO :                  *
000600*     - TEST THE ROUTINE OF MONEY CONVERSION      *
000700***************************************************
000800 ENVIRONMENT DIVISION.
000900 CONFIGURATION SECTION.
001000 SPECIAL-NAMES.
001100         DECIMAL-POINT IS COMMA.
001200
001300 DATA DIVISION.
001400 WORKING-STORAGE SECTION.
001500***   CHGBOOK IS A COPYBOOK CONTAINING ARRAY OF TEST
001600***   FOR VARIOUS MONEY IN EUROPE AND ASIA MARKET
001700 COPY CHGBOOK.
001800***   BAD-CODES - STRING USED TO TEST ERROR MESSAGE
001900***   RETURNED BY OUR ROUTINE PROGRAM CONVERT
002000 77 BAD-CODES           PIC X(6)  VALUE 'KOLMUI'.
002100
002110 01 LIB                 PIC X(20).
002200 01 L-SEP               PIC X(30) VALUE ALL '*'.
002300 01 RESULT              PIC 9(3)V9(2).
002400 01 EXPECTED            PIC 9(3)V9(2).
002500 01 EXPECTED-MSG        PIC X(20) VALUE 'MONEY CODE NOT FOUND'.
002600
002700** FOR CALLING & HANDLING ROUTINE RESPONSE
002710 01 LK-DEV-CODE         PIC X(2).
002720 01 LK-PRICE            PIC 9(3)V99.
002730 01 LK-MSG              PIC X(20) VALUE SPACES.
002740
002750 01 IDX                 PIC 99    VALUE 0.
002760 01 RATIO               PIC 9(2)V9(2).
002770 01 ED-RATIO            PIC Z9,99.
002780***********************
002790 LINKAGE SECTION.
002791 COPY TESTCONT.
002792**********************************
002793
002794 PROCEDURE DIVISION USING TEST-CONTEXT.
002795
002796      PERFORM ALL-CURRENCIES
002797      PERFORM INVALID-CODES
002799
002800      PERFORM SUMMARY
002801
002802      GOBACK.
002803* PARAGRAPHS *
002804***************************************************************
002805*  ALL-CURRENCIES CALLS ALL VALID CODES FROM OUR KSDS FILES   *
002806*  AND CHECK PROPER CONVERSION                                *
002807***************************************************************
002808 ALL-CURRENCIES.
002809      DISPLAY L-SEP
002810      DISPLAY '---- ALL CURRENCIES TEST -----'
002820      PERFORM VARYING IDX FROM 1 BY 1 UNTIL IDX > 9
002830         INITIALIZE RESULT
002840         MOVE 'TEST-'        TO LIB
002850         MOVE TLIB(IDX)      TO LIB(6:13)
002860         MOVE TCODE(IDX)     TO LK-DEV-CODE
002870         MOVE TAMOUNT(IDX)   TO LK-PRICE
002880         MOVE TEXPECT(IDX)   TO EXPECTED
002890
002900         CALL 'CONVERT' USING  LK-DEV-CODE,
003000                               LK-PRICE,
003100                               LK-MSG
003200
003300         CALL 'ASSERTER'
003400               USING TEST-CONTEXT, LIB, EXPECTED, LK-PRICE
003500
003600         DISPLAY 'LIB :' LIB
003700         DISPLAY 'RUN ', TESTS-RUN, ',OK ', PASSES,
003800                                    ',KO ', FAILURES
003900         DISPLAY L-SEP
004000
004100      END-PERFORM
004200      .
004300
004400***************************************************************
004500*  INVALID-CODES CALLS THE KSDS WITH INVALID DEVISE CODES     *
004600*  AND CHECK EXPECTED ERROR MESSAGE WHEN DOING SO             *
004700***************************************************************
004800 INVALID-CODES.
004900     DISPLAY L-SEP
005000     DISPLAY '---- INVALID CODES TEST -----'
005100     INITIALIZE IDX LK-DEV-CODE LK-PRICE
005200     PERFORM VARYING IDX FROM 1 BY 2 UNTIL
005300               IDX > LENGTH OF BAD-CODES
005400         MOVE 'TEST-INVALID-CODES' TO LIB
005500         MOVE 100,00            TO LK-PRICE
005600         MOVE BAD-CODES(IDX:2)  TO LK-DEV-CODE
005700
005800         CALL 'CONVERT' USING LK-DEV-CODE,
005900                              LK-PRICE,
006000                              LK-MSG
006100
006200         DISPLAY 'LOOKING FOR : ' LK-DEV-CODE
006300         CALL 'MSGASSER' USING TEST-CONTEXT, LIB,
006400               EXPECTED-MSG, LK-MSG
006500         DISPLAY 'LIB :' LIB
006600         DISPLAY 'RUN ', TESTS-RUN, ',OK ', PASSES,
006700                       ',KO ', FAILURES
006800         DISPLAY L-SEP
006900
007000     END-PERFORM
007100     .
007200 SUMMARY.
007300     DISPLAY L-SEP
007400     DISPLAY 'TOTAL SUCCESS - ' PASSES
007500     DISPLAY 'TOTAL FAILURES - ' FAILURES
007600     COMPUTE RATIO = PASSES / (FAILURES + PASSES)
007700     MOVE RATIO TO ED-RATIO
007800     DISPLAY ED-RATIO '% OF SUCCESS TEST COVERAGE'
007900     .
008000
008100
008200
008300
008400
008500
008600
008700
008800
008900
009000
009100
009200
009300
009400
009500
009600
009700
009800
009900
010000
010100
010200
010300
010400
010500
010600
010700
010800
010900
011000
011100
011200
011300
