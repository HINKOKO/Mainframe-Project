000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. EXTORDER.
000300
000400 ENVIRONMENT DIVISION.
000500 CONFIGURATION SECTION.
000600 SPECIAL-NAMES.
000700     DECIMAL-POINT IS COMMA.
000800
000900 INPUT-OUTPUT SECTION.
001000 FILE-CONTROL.
001100     SELECT FEXT ASSIGN TO FICEXT.
001310
001320 DATA DIVISION.
001400 FILE SECTION.
001410* DECLARATION ENREGISTREMENTS FICHIER
001610 FD FEXT.
001620 01 ENR-FEXT PIC X(299).
001900
002000 WORKING-STORAGE SECTION.
002001******************************************
002002* INSERT DES DECLARATIONS SQL
002003     EXEC SQL INCLUDE SQLCA  END-EXEC.
002004     EXEC SQL INCLUDE ITE    END-EXEC.
002005     EXEC SQL INCLUDE PRO    END-EXEC.
002006     EXEC SQL INCLUDE ORD    END-EXEC.
002007     EXEC SQL INCLUDE CUS    END-EXEC.
002008     EXEC SQL INCLUDE EMP    END-EXEC.
002009     EXEC SQL INCLUDE DEP    END-EXEC.
002017******************************************
002018* DECLARATION DU CURSEUR ORDERS
002019     EXEC SQL
002020         DECLARE CURS CURSOR
002021         FOR
002022         SELECT
002023         I.P_NO, I.QUANTITY, I.PRICE,
002024         P.DESCRIPTION,
002025         O.O_NO, O.O_DATE,
002026         C.COMPANY, C.ADDRESS ,C.CITY ,C.ZIP, C.STATE,
002027         E.LNAME, E.FNAME,
002028         D.DNAME
002029         FROM API10.ITEMS I
002030         JOIN API10.PRODUCTS P ON P.P_NO = I.P_NO
002031         JOIN API10.ORDERS O ON O.O_NO = I.O_NO
002032         JOIN API10.CUSTOMERS C ON C.C_NO = O.C_NO
002033         JOIN API10.EMPLOYEES E ON E.E_NO = O.S_NO
002034         JOIN API10.DEPTS D ON D.DEPT = E.DEPT
002035         ORDER BY O.O_NO ASC, I.P_NO ASC
002036     END-EXEC.
002037
002038* VARIABLES FONCTION ABEND-PROG
002039 77 WS-ANO PIC 9 VALUE ZERO.
002040 77 WS-VAR PIC 9 VALUE ZERO.
002041
002314* VARIABLES EDITION
002326 77 WR-ITE-QUANTITY PIC X(2).
002327* ITE-PRICE DCLGEN  PIC S9(3)V9(2) USAGE COMP-3.
002328 77 WR-ITE-PRICE    PIC X(6).
002329 77 NC-ITE-PRICE    PIC S9(3)V9(2).
002330 77 ED-ITE-PRICE    PIC 999V99.
002331 77 WR-ORD-O-NO     PIC X(3).
002340 77 WR-DATAF        PIC X(299).
002444
002445 01 LIGNE.
002446    05 L-P-NO        PIC X(3) .
002447    05 L-QUANTITY    PIC X(2) .
002448    05 L-PRICE       PIC X(6) .
002449    05 L-DESCRIPTION PIC X(30) .
002450    05 L-O-NO        PIC X(3) .
002451    05 L-O-DATE      PIC X(10).
002452    05 L-COMPANY     PIC X(30) .
002453    05 L-ADDRESS     PIC X(100) .
002454    05 L-CITY        PIC X(20) .
002455    05 L-ZIP         PIC X(5) .
002456    05 L-STATE       PIC X(2) .
002457    05 L-LNAME       PIC X(20).
002458    05 L-FNAME       PIC X(20).
002459    05 L-DNAME       PIC X(20).
002460    05 FILLER        PIC X(28).
002461* LONGUEUR = 270 + FILLER 29 --> FICHIER 300
002462
002463 PROCEDURE DIVISION.
002464* PARTIE 1 ALIMENTATION DU FICHIER EXTRACT.DATA
002470      PERFORM OPEN-FEXT
002700      PERFORM OPEN-CURS
002710      PERFORM FETCH-CURS
002800      PERFORM TEST-SQLCODE
003000      PERFORM UNTIL SQLCODE NOT EQUAL ZERO
003010         PERFORM WRITE-FEXT
003020         PERFORM FETCH-CURS
003030         PERFORM TEST-SQLCODE
003700      END-PERFORM
003900      PERFORM CLOSE-CURS
004000      PERFORM CLOSE-FEXT
004010* PARTIE 2 ALIMENTATION DU FICHIER FACTURES.DATA
004100      GOBACK.
004110********************************************
004120* PARAGRAPHES
004200********************************************
004760 CLOSE-FEXT.
004770     CLOSE FEXT.
004780 OPEN-FEXT.
004790     OPEN OUTPUT FEXT.
004791 CLOSE-CURS.
004792     EXEC SQL CLOSE CURS END-EXEC.
004793 OPEN-CURS.
004794     EXEC SQL OPEN CURS END-EXEC.
004795 FETCH-CURS.
004796     EXEC SQL
004797         FETCH CURS
004798         INTO
004799         :ITE-P-NO,
004800         :ITE-QUANTITY,
004801         :ITE-PRICE,
004802         :PRO-DESCRIPTION,
004803         :ORD-O-NO,
004804         :ORD-O-DATE,
004805         :CUS-COMPANY,
004806         :CUS-ADDRESS,
004807         :CUS-CITY,
004808         :CUS-ZIP,
004809         :CUS-STATE,
004810         :EMP-LNAME,
004811         :EMP-FNAME,
004812         :DEP-DNAME
004813     END-EXEC.
004814 TEST-SQLCODE.
004815     EVALUATE TRUE
004816         WHEN SQLCODE = ZERO
004817              CONTINUE
004818         WHEN SQLCODE > ZERO
004819              DISPLAY 'WARNING : ' SQLCODE
004820         WHEN OTHER
004821              DISPLAY 'ANOMALIE GRAVE : ' SQLCODE
004822              PERFORM ABEND-PROG
004823     END-EVALUATE.
004829 ABEND-PROG.
004830     COMPUTE WS-ANO = WS-ANO / WS-VAR.
009000 WRITE-FEXT.
009100* TRAITEMENT COMPATIBILITE DONNEES
009110     MOVE ITE-QUANTITY      TO WR-ITE-QUANTITY
009122     MOVE ITE-PRICE         TO ED-ITE-PRICE
009124     MOVE ED-ITE-PRICE(1:3) TO WR-ITE-PRICE(1:3)
009125     MOVE ','               TO WR-ITE-PRICE(4:1)
009126     MOVE ED-ITE-PRICE(4:2) TO WR-ITE-PRICE(5:2)
009130     MOVE ORD-O-NO          TO WR-ORD-O-NO
009140* ALIM LIGNE AVEC VARIABLES DU FETCH
009150     MOVE ITE-P-NO                TO L-P-NO
009160     MOVE WR-ITE-QUANTITY         TO L-QUANTITY
009170     MOVE WR-ITE-PRICE            TO L-PRICE
009180     MOVE PRO-DESCRIPTION-TEXT    TO L-DESCRIPTION
009190     MOVE WR-ORD-O-NO             TO L-O-NO
009191     MOVE ORD-O-DATE              TO L-O-DATE
009192     MOVE CUS-COMPANY-TEXT        TO L-COMPANY
009193     MOVE CUS-ADDRESS-TEXT        TO L-ADDRESS
009194     MOVE CUS-CITY-TEXT           TO L-CITY
009195     MOVE CUS-ZIP                 TO L-ZIP
009196     MOVE CUS-STATE               TO L-STATE
009197     MOVE EMP-LNAME-TEXT          TO L-LNAME
009198     MOVE EMP-FNAME-TEXT          TO L-FNAME
009200     MOVE DEP-DNAME-TEXT          TO L-DNAME
009300     WRITE ENR-FEXT FROM LIGNE BEFORE ADVANCING 1 LINES.
